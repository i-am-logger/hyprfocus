{
  description = "Monochromatic screen overlay for Hyprland";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
  };

  outputs =
    { self, nixpkgs }:
    let
      supportedSystems = [
        "x86_64-linux"
        "aarch64-linux"
      ];
      forAllSystems = nixpkgs.lib.genAttrs supportedSystems;
      cargoToml = builtins.fromTOML (builtins.readFile ./Cargo.toml);
    in
    {
      # Build with consumer's pkgs so NixOS allowUnfreePredicate applies
      overlays.default = final: _prev: {
        hypr-vogix = final.rustPlatform.buildRustPackage {
          pname = cargoToml.package.name;
          version = cargoToml.package.version;
          src = self;
          cargoLock.lockFile = "${self}/Cargo.lock";
          meta = {
            description = cargoToml.package.description;
            homepage = cargoToml.package.repository;
            license = final.lib.licenses.cc-by-nc-sa-40;
            mainProgram = "hypr-vogix";
          };
        };
      };

      packages = forAllSystems (
        system:
        let
          pkgs = nixpkgs.legacyPackages.${system};
        in
        {
          default = pkgs.rustPlatform.buildRustPackage {
            pname = cargoToml.package.name;
            version = cargoToml.package.version;
            src = ./.;
            cargoLock.lockFile = ./Cargo.lock;

            meta = {
              description = cargoToml.package.description;
              homepage = cargoToml.package.repository;
              license = pkgs.lib.licenses.cc-by-nc-sa-40;
              mainProgram = "hypr-vogix";
            };
          };
        }
      );
    };
}
